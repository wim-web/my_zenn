---
title: "Terraformã§ãƒã‚¹ãƒˆã—ãŸloopã‚’æ›¸ã"
emoji: "ğŸ‘»"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["Terraform"]
published: true
---

## ç’°å¢ƒ

```bash
$ terraform -v
Terraform v1.4.2
on darwin_arm64
```

## ä¾‹é¡Œ

[aws_ssoadmin_account_assignment](https://registry.terraform.io/providers/hashicorp/aws/latest/docs/resources/ssoadmin_account_assignment)ã‚’ä¾‹ã«è€ƒãˆã¦ã¿ã¾ã™ã€‚

aws_ssoadmin_account_assignmentãƒªã‚½ãƒ¼ã‚¹ã¯AWSã‚¢ã‚«ã‚¦ãƒ³ãƒˆã¨SSOã‚°ãƒ«ãƒ¼ãƒ—ã¨è¨±å¯ã‚»ãƒƒãƒˆã‚’ç´ä»˜ã‘ã‚‹ã‚‚ã®ã§ã™ã€‚

ã‚ã‚‹AWSã‚¢ã‚«ã‚¦ãƒ³ãƒˆã«è¤‡æ•°ã®ã‚°ãƒ«ãƒ¼ãƒ—ã«è¤‡æ•°ã®è¨±å¯ã‚»ãƒƒãƒˆã‚’ç´ä»˜ã‘ã‚ˆã†ã¨ã™ã‚‹ã¨å˜ç´”ã«for_eachã§å›ã›ãªã„ã“ã¨ã«æ°—ã¥ãã¾ã™ã€‚



```hcl
locals {
  groups = {
    "GROUP_A" = {
      permission_set_arns = [
        "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7c",
        "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7b"
      ]
    }
    "GROUP_B" = {
      permission_set_arns = [
        "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7c"
      ]
    }
  }
}

resource "aws_ssoadmin_account_assignment" "this" {
  for_each           = local.groups
  instance_arn       = "instance_arn"
  # ã“ã“ã¯æ–‡å­—åˆ—ã‚’æœŸå¾…ã—ã¦ã„ã‚‹ã®ã§permission_set_arnsã‚’æ¸¡ã›ãªã„
  # ã‹ã¨ã„ã£ã¦forã‚„for_eachã‚’ä½¿ãˆãªã„...
  permission_set_arn = each.value.permission_sets
  principal_id       = each.key
  principal_type     = "GROUP"
  target_id          = "123456789012"
  target_type        = "AWS_ACCOUNT"
}
```

ä»¥ä¸Šã‹ã‚‰ã‚ã‹ã‚‹ã‚ˆã†ã«terraformã®for_eachã§ã¯1é‡ã®loopã—ã‹è¡¨ç¾ã§ããªã„ã“ã¨ãŒã‚ã‹ã‚Šã¾ã™ã€‚

```
# ã“ã‚Œã¯è¡¨ç¾ã§ãã‚‹
for {
    # todo
}

# ã“ã‚Œã¯è¡¨ç¾ã§ããªã„
for {
    for {
        # todo
    }
}
```

terraformã§ãƒã‚¹ãƒˆã—ãŸloopã‚’è¡¨ç¾ã™ã‚‹ã«ã¯ã„ãã¤ã‹æ–¹æ³•ãŒã‚ã‚Šã¾ã™ã€‚

## moduleã‚’ä½¿ã†

ãƒªã‚½ãƒ¼ã‚¹ã‚’moduleã«ç§»å‹•ã—ã¦ã€moduleã¨resourceã§for_eachã‚’ä½¿ã†ã“ã¨ã§ãƒã‚¹ãƒˆã—ãŸloopã‚’è¡¨ç¾ã—ã¾ã™ã€‚

```hcl
# main.tf
locals {
  groups = {
    "GROUP_A" = {
      permission_set_arns = [
        "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7c",
        "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7b"
      ]
    }
    "GROUP_B" = {
      permission_set_arns = [
        "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7c"
      ]
    }
  }
}


module "group_permissions" {
  # ã“ã“ã§loop
  for_each            = local.groups
  source              = "./modules/group_permissions/"
  permission_set_arns = each.value.permission_set_arns
  group_id            = each.key
  aws_account_id      = "123456789012"
}
```

```hcl
# module
variable "permission_set_arns" {
  type = set(string)
}

variable "group_id" {
  type = string
}

variable "aws_account_id" {
  type = string
}

resource "aws_ssoadmin_account_assignment" "this" {
  # ã“ã“ã§ã•ã‚‰ã«loop
  for_each           = var.permission_set_arns
  instance_arn       = "instance_arn"
  permission_set_arn = each.value
  principal_id       = var.group_id
  principal_type     = "GROUP"
  target_id          = var.aws_account_id
  target_type        = "AWS_ACCOUNT"
}
```

moduleã«ã‚ã‘ã‚‹ã‚„ã‚Šæ–¹ãŒä¸€ç•ªç›´æ„Ÿçš„ã«ã‚ã‹ã‚Šã‚„ã™ã„ã¨æ€ã„ã¾ã™ã€‚  
moduleå†…ã§ã•ã‚‰ã«moduleã‚’ã¤ãã‚‹ã“ã¨ã§ä½•é‡ã®loopã«ã‚‚å¯¾å¿œå¯èƒ½ã§ã™ã€‚

## flattenã™ã‚‹

for_eachã§å›ã™å¤‰æ•°ãŒå¤šæ¬¡å…ƒã§ã‚ã‚‹ã“ã¨ãŒæ‚ªã„ã®ã§ã€flattenã§ä¸€æ¬¡å…ƒåŒ–ã—ã¦ã‚ã’ã¦ãã‚Œã‚’for_eachã§å›ã—ã¾ã™ã€‚

```hcl
locals {
  groups = {
    "GROUP_A" = {
      permision_set_arns = [
        "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7c",
        "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7b"
      ]
    }
    "GROUP_B" = {
      permision_set_arns = [
        "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7c"
      ]
    }
  }
}

locals {
  flatten_groups = flatten([
    for group, ps in local.groups : [
      for p in ps.permision_set_arns : {
        group_id      = group
        permission_id = p
      }
    ]
  ])
}

resource "aws_ssoadmin_account_assignment" "this" {
  for_each           = { for f in local.flatten_groups : "${f.group_id}_${f.permission_id}" => f }
  instance_arn       = "instance_arn"
  permission_set_arn = each.value.permission_id
  principal_id       = each.value.group_id
  principal_type     = "GROUP"
  target_id          = "123456789012"
  target_type        = "AWS_ACCOUNT"
}
```

flatten_groupsã®ä¸­èº«ã¯ä»¥ä¸‹ã®ã‚ˆã†ã«ãªã£ã¦ã„ã¾ã™ã€‚

```hcl
flatten_groups = [
    {
        group_id      = "GROUP_A"
        permission_id = "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7c"
    },
    {
        group_id      = "GROUP_A"
        permission_id = "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7b"
    },
    {
        group_id      = "GROUP_B"
        permission_id = "arn:aws:sso:::permissionSet/ssoins-12345e7cccc04b03/bs-6bdaf3a33d689a7c"
    },
]
```
